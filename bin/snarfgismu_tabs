#!/usr/bin/perl

# All input files must match the following format.
# 1. All values must be seperated by Tabs
# 2. rafsi should not be included
# 3. The four columns should be in the following order:
#    a) gismu
#    b) glossword
#    c) definition
#    d) notes
#    e) optionally, one or more place keywords, in order
# 4. The definition and the notes must already be jbovlaste-ified;
# this means that the definition must have LaTeX-style $x_1$ places
# in it, and the notes field must have cross-references like
# {bacru}.
#
# The import will fail if the above conditions are not met.
#

use strict;
use warnings;

use DBI;

use FindBin;
use lib "$FindBin::Bin/../lib";
use db;

$dbh->{RaiseError} = 1;

my $time = time();

# I can imagine wanting to change this, but not enough to make it a
# parameter
my $votevalue = 1;
my $defnum = 1;
my $meaningnum = 1;

use Getopt::Long;
my $file = undef;
my $userid = undef;
my $lang = undef;
my $result = GetOptions ("file=s" => \$file, "userid=i" => \$userid, "lang=i"   => \$lang );
my $deluserid = $userid;

if( ! $userid || ! $lang || ! $file ) {
  print "usage: -f data_file -u userid -l langid\n";
  exit 1;
}

open(GISMU,"<", $file);
while(<GISMU>) {
  chomp;
  my @columns = split(/\t/);

  if( (scalar @columns) < 4 ) {
    print "SKIPPING line with insufficient number of columns: $_\n";
    next;
  }

  # Clean out spaces.
  map { s/^\s+// } (map { s/\s+$// } @columns);

  my($gismu,$glossword,$definition,$notes,@keywords) = @columns;

  # treat the glossword as a keyword with place 0
  unshift @keywords, $glossword;

  $gismu =~ tr/{}//d;
  $gismu =~ s/^\.//g;

  #####
  #Check for proper definition and notes formatting.
  #####
  if (  $definition !~ /\$x_(\{1\}|1)\$/ ) {
    print "The definition for \"$gismu\", \"$definition\" is not properly jbovlaste-ified. It should have places of the format \$x_{1}\$ or similar. SKIPPING.\n";
    next;
  }

  if ( $notes and $notes !~ /\{.....\}/) {
    print "The gismu cross references in the notes for \"$gismu\", \"$notes\", are not formatted properly. Please see that all gismu are enclosed in braces. e.g {basti}.  SKIPPING.\n";
    next;
  }

  next if $gismu =~ /\d/;

  print "$gismu###$keywords[0]###$definition###$notes\n\n";

  ####
  # Retrieve the basic valsi information (should already be in the
  # DB)
  ####

  my $valsiid = $dbh->selectrow_array("SELECT valsiid FROM valsi WHERE word=?", undef, $gismu);

  if( ! $valsiid )
  {
    print "This script is not designed to add new gismu (or any valsi, for that matter), but I can't find an entry for $gismu ; bailing.\n";
    exit 1
  }

  ####
  # Delete old versions
  ####

  $dbh->begin_work;

  my $definitionidsref = $dbh->selectall_arrayref("SELECT definitionid from definitions WHERE langid=? AND valsiid=? AND userid=?",
    undef,
    $lang, $valsiid, $deluserid );

  foreach my $definitionidref (@{$definitionidsref}) {
    my $defid = @{$definitionidref}[0];

    print "\nDefinition id $defid found for valsi id $valsiid, deletions beginning.\nPlease ignore any errors about referential integrity.\n\n";

    $dbh->do("DELETE FROM keywordmapping WHERE definitionid=?", undef, $defid );
    $dbh->do("DELETE FROM natlangwordvotes WHERE definitionid=?", undef, $defid);
    $dbh->do("DELETE FROM definitionvotes WHERE definitionid=?", undef, $defid);
    $dbh->do("DELETE FROM definitions WHERE definitionid=?", undef, $defid );
  }

  foreach my $keyword (@keywords) {
    my $nlwidsref = $dbh->selectall_arrayref("SELECT wordid from natlangwords WHERE word=? AND langid=? AND userid=?",
      undef,
      $keyword, $lang, $deluserid );

    foreach my $nlwidref (@{$nlwidsref}) {
      my $nlwid = @{$nlwidref}[0];

      print "\nNatlang word id $nlwid found for word id $keyword, deletions beginning.\nPlease ignore any errors about referential integrity.\n\n";

      $dbh->do("DELETE FROM keywordmapping WHERE natlangwordid=?", undef, $nlwid );
      $dbh->do("DELETE FROM natlangwordvotes WHERE natlangwordid=?", undef, $nlwid);
      $dbh->do("DELETE FROM natlangwords WHERE wordid=?", undef, $nlwid); 
    }
  }

  ####
  # Enter the definition information.
  ####

  $dbh->do("INSERT INTO definitions (langid, valsiid, definitionNum, definition, notes, userid, time) VALUES
    (?, ?, ?, ?, ?, ?, ?)",
    undef, $lang, $valsiid, $defnum, $definition, $notes, $userid, $time);

  # Retrieve the DefinitionID that got generated above.
  my $defid = $dbh->selectrow_array("SELECT definitionid from definitions WHERE langid=? AND valsiid=? AND definitionNum=?",
    undef,
    $lang, $valsiid, $defnum );

  print "DEFID: $defid\n";

  ####
  # Enter the vote information.
  ####
  $dbh->do("INSERT INTO definitionvotes (langid, valsiid, definitionId, value, userid, time)
    VALUES
    (?,
    ?,
    ?,
    ?,
    ?,
    ?)",
    { },
    $lang,
    $valsiid,
    $defid,
    $votevalue,
    $userid,
    $time);

  for (my $i = 0; $i <= 5; $i++) {
    my $keyword = $keywords[$i];

    if( ! $keyword ) {
      next;
    }

    ####
    # Try to find our word before we add it.
    ####
    my $wordid = $dbh->selectrow_array("SELECT wordid from natlangwords WHERE word=? AND langid=? AND meaning is null",
      undef,
      $keyword, $lang );

    print "Pre-found WORDID: $wordid\n";

    if( ! $wordid ) {

      ####
      # Enter the basic natlang word information for the keyword
      ####
      $dbh->do("INSERT INTO natlangwords (langid, word, meaningNum, userid, time ) VALUES (?, ?, ?, ?, ?)",
        undef,
        $lang, $keyword, $meaningnum, $userid, $time );

      ####
      # Retrieve the WordID that got generated above.
      ####
      $wordid = $dbh->selectrow_array("SELECT wordid from natlangwords WHERE word=? AND langid=? AND meaning is null",
        undef,
        $keyword, $lang );

      print "Just-created WORDID: $wordid\n";

    }

    my $vote = $dbh->selectrow_array("SELECT 1 FROM natlangwordvotes WHERE natlangwordid=? AND userid=?",
      undef,
      $wordid, $userid );
    unless ($vote) {
      ####
      # Enter the vote information.
      ####
      $dbh->do("INSERT INTO natlangwordvotes (definitionid, natlangwordid, place, value, userid, time)
        VALUES
        (?,
        ?,
        ?,
        ?,
        ?,
        ?)",
        undef,
        $defid,
        $wordid,
        $i,
        $votevalue,
        $userid,
        $time);
    }

    ####
    # Enter the mapping from the keyword to the definition.
    ####
    $dbh->do("INSERT INTO keywordmapping (definitionId, natlangwordId, place ) VALUES
      (
      ?,
      ?,
      ?
      )",
      undef,
      $defid, $wordid, $i );

  }

  $dbh->commit;
}

$dbh->disconnect;

#***************************
# OLD to-LaTeX/Wiki conversion bits.
#***************************
#
# These are no longer used, but may provide assistance if a user
# needs help with data formatting.  These were used to convert raw
# English gismu formatting into the stuff that jbovlaste needs.
#
#	# Turn x1 into LaTex format $x_{1}$.  The HTML output engine
#	# should turn that LaTeX pattern into nice HTML.
#	$definition =~ s/x(\d)/\$x_{$1}\$/g;
#
#        ####
#        # Now we try to turn the notes into something that approximates
#        # valid Wiki text.
#        ####
#        # ka[ ... ],
#        $notes =~ s/([a-z'])\[([^][]*)\]([,.])/$1 <$2>$3/g;
#        # [x3 sides/surfaces should ... ],
#        $notes =~ s/\[(x[^][]*)\][;, ]/$1. /g;
#        # [indicates a subjective greatness, ... ; terrible (mabla)];
#        $notes =~ s/\[(.)([^][]*)\][;, ]/uc($1).$2.". "/ge;
#        # [ x3 ... ](
#        $notes =~ s/\[(x[^][]*)\]\(/$1 (/g;
#        # [ foo ... ](
#        $notes =~ s/\[(.)([^][]*)\]\(/uc($1).$2." ("/ge;
#        # [ x3 ... ]<EOL>
#        $notes =~ s/\[(x[^][]*)\]$/$1. /g;
#        # [ foo ... ]<EOL>
#        $notes =~ s/\[(.)([^][]*)\]$/uc($1).$2.". "/ge;
#	# Remaning []s.
#	$notes =~ s/\[/\(/g;
#	$notes =~ s/\]/\)/g;
#
#	#####
#	# Handle x1 things.
#	#####
#	$notes =~ s/x(\d)(\W)/\$x_$1\$$2/g;
#	$notes =~ s/(\W)x(\d)/$1\$x_$2\$/g;
#
#        # This version assumes that the user has done the {...} in
#        # and "see also" and such themselves; massage the input data
#        # as needed in vim or something.
#
#        my $lastnotes="";
#
#        while( $lastnotes ne $notes )
#        {
#            $lastnotes = $notes;
#
#            # (= foo, foo, foo, ...  )
#            if( $notes =~ s/\(\s*=\s+([a-z', ]+)\)/(= ####)/ )
#            {
#             my $notespart = $1;
#
#                # manci,
#                $notespart =~ s/([a-z']+), /{$1}, /g;
#
#                # manci
#                $notespart =~ s/([a-z']+)\s*$/{$1}/g;
#
#                $notes =~ s/####/$notespart/;
#            }
#        }
#
#        # (cf. foo, foo, foo, ...  )
#        if( $notes =~ s/\(kp\.\s+([a-z', ]+)\)/Vidu anka≈≠ ####./ )
#        {
#            my $notespart = $1;
#
#            # manci,
#            $notespart =~ s/([a-z']+), /{$1}, /g;
#
#            # manci
#            $notespart =~ s/([a-z']+)\s*$/{$1}/g;
#
#            $notes =~ s/####/$notespart/;
#        }
#
